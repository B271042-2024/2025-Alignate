#!/usr/bin/env tcsh
echo '#  Running runpsipred_single...'

# Setup paths
set cwd = `dirname $0`
set execdir = "$cwd/bin"
set datadir = "$cwd/data"

# Input FASTA file
set filename = "$1"
set basename = `basename "$filename"`
set rootname = `echo "$basename" | sed 's/\.[^.]*$//'`
set outname = "${rootname}.chk"

# Unique temp root
set hostid = `hostid`
set tmproot = psitmp$$$hostid

#echo "Generating mtx file from sequence: $filename"
$execdir/seq2mtx "$filename" > "$tmproot.mtx"
if ($status != 0) then
    echo "FATAL: Error whilst running seq2mtx - script terminated!"
    exit 1
endif

#echo "Predicting secondary structure based on single sequence ..."
#echo "Pass1 ..."
$execdir/psipred "$tmproot.mtx" "$datadir/weights.dat" "$datadir/weights.dat2" "$datadir/weights.dat3" > "$rootname.ss"
if ($status != 0) then
    echo "FATAL: Error whilst running psipred - script terminated!"
    exit 1
endif

#echo "Pass2 ..."
$execdir/psipass2 "$datadir/weights_p2.dat" 1 1.0 1.0 "$rootname.ss2" "$rootname.ss" > "$rootname.horiz"
if ($status != 0) then
    echo "FATAL: Error whilst running psipass2 - script terminated!"
    exit 1
endif

#echo "Cleaning up ..."
\rm -f "$tmproot".* error.log

echo "#  Final output files: $rootname.ss2 $rootname.horiz"
#echo "Finished."
